#!ipxe
#boot.salstar.sk

menu BSD systems
item --key o openbsd OpenBSD [o]
item --key n netbsd NetBSD [n]
item --key f freebsd FreeBSD [f]
item --key p pcbsd PC-BSD [p]
choose bsd || goto bsd_exit

goto ${bsd}
:bsd_exit
exit 0

:boot_bsd
#echo ${cls}
imgfree
#imgtrust --allow
initrd ${mirror}${dir}/${iso} || initrd ${mirror}${dir}/${iso2}
set url http://boot.salstar.sk/
chain ${url}memdisk iso raw
exit

:openbsd
set os OpenBSD
menu ${os} ${archl}
item 5.3 ${os} 5.3
item 5.2 ${os} 5.2
item 5.1 ${os} 5.1
item 5.0 ${os} 5.0
item 4.9 ${os} 4.9
item 4.8 ${os} 4.8
item other ${os} Choose other version
choose ver || exit 0

iseq ${ver} other || goto skip_read_ver_openbsd
echo ${cls}
echo -n Enter version: ${} && read ver
:skip_read_ver_openbsd

iseq ${ver} 5.3 && set bver 53 ||
iseq ${ver} 5.2 && set bver 52 ||
iseq ${ver} 5.1 && set bver 51 ||
iseq ${ver} 5.0 && set bver 50 ||
iseq ${ver} 4.9 && set bver 49 ||
iseq ${ver} 4.8 && set bver 48 ||

set mirror http://ftp.eu.openbsd.org/pub/
set dir OpenBSD/${ver}/${archl}
set iso cd${bver}.iso
goto boot_bsd

:netbsd
set os NetBSD
menu ${os} ${arch_a}
item 6.1 ${os} 6.1
item 6.0.1 ${os} 6.0.1
item 5.2 ${os} 5.2
item 5.1.2 ${os} 5.1.2
item 5.0.2 ${os} 5.0.2
item 4.0.1 ${os} 4.0.1
item other ${os} Choose other version
choose ver || exit 0

iseq ${ver} other || goto skip_read_ver_netbsd
echo ${cls}
echo -n Enter version: ${} && read ver
:skip_read_ver_netbsd

set mirror http://iso.se.netbsd.org/pub/
set dir NetBSD/iso/${ver}
set iso2 ${arch_a}cd-${ver}.iso
set iso NetBSD-${ver}-${arch_a}.iso
goto boot_bsd

:freebsd
set os FreeBSD
menu ${os}
item 9.1 ${os} 9.1
item 9.0 ${os} 9.0
item other ${os} Choose other version
choose ver || exit 0

iseq ${ver} other || goto skip_read_ver_freebsd
echo ${cls}
echo -n Enter version: ${} && read ver
:skip_read_ver_freebsd

set mirror http://ftp.se.freebsd.org/pub/
set dir FreeBSD/releases/ISO-IMAGES/${ver}
#set iso FreeBSD-${ver}-RELEASE-${archl}-bootonly.iso
set iso FreeBSD-${ver}-RELEASE-${archl}-memstick.img
goto boot_bsd

:pcbsd
set os PC-BSD
menu ${os}
item 9.0 ${os} 9.0
item 8.2 ${os} 8.2
item other ${os} Choose other version
choose ver || exit 0

iseq ${ver} other || goto skip_read_ver_pcbsd
echo ${cls}
echo -n Enter version: ${} && read ver
:skip_read_ver_pcbsd

set mirror ftp://ftp.df.lth.se/pub/PCBSD/
#iseq ${arch} i386 && set xarch x86 || set xarch x64
set dir ${ver}/${archl}
set iso PCBSD${ver}-${archl}-bootonly.iso
goto boot_bsd
